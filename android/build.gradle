buildscript {
    ext {
        kotlin_version = "1.9.10"
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.1'
        classpath 'com.google.gms:google-services:4.3.15'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://raw.githubusercontent.com/guardianproject/gpmaven/master" 
                url 'https://storage.googleapis.com/download.flutter.io'
        }
        flatDir {
            dirs 'libs'
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    project.logger.error(
                            "Warning: 'namespace' property for Flutter plugin '${project.name}' is not defined."
                                    + " Setting the 'namespace' property to  '${project.group}' as a fallback."
                                    + "\nWith AGP 8.+, the 'namespace' property must be explicitly defined in the module-level build script."
                                    + "\nIf a new version of '${project.name}' is not available, consider filing an issue against '${project.name}'"
                                    + " to add 'namespace' property on their module-level build script (otherwise try updating to the latest version)."
                    )
                    namespace project.group
                }
            }
            Integer pluginCompileSdk = project.android.compileSdk
            if ((project.plugins.hasPlugin("com.android.application") || project.plugins.hasPlugin("com.android.library")) && pluginCompileSdk != null && pluginCompileSdk < 31) {
                project.logger.error(
                        "Warning: Overriding 'compileSdkVersion' version on Flutter plugin '${project.name}' from "
                                + pluginCompileSdk
                                + " to 35 (to work around https://issuetracker.google.com/issues/199180389)."
                                + "\nIf a new version of '${project.name}' is not available, consider filing an issue against '${project.name}'"
                                + " to increase their compileSdkVersion to the latest (otherwise try updating to the latest version)."
                )
                project.android {
                    compileSdkVersion 35
                    buildToolsVersion "35.0.0"
                }
            }
        }
    }

    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(':app')
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
